buildscript {
  ext {
    indraVersion = "2.0.6"
  }
}

plugins {
  id "net.kyori.indra" version "$indraVersion" apply false
  id "net.kyori.indra.checkstyle" version "$indraVersion" apply false
  id "net.kyori.indra.license-header" version "$indraVersion" apply false
  id "net.kyori.indra.publishing.sonatype" version "$indraVersion"
}

group 'net.kyori'
version '1.3.0'
description "A library for examining objects and producing an output - a friendly way to create a toString result."

subprojects {
  apply plugin: "net.kyori.indra"
  apply plugin: "net.kyori.indra.checkstyle"
  apply plugin: "net.kyori.indra.license-header"
  apply plugin: "net.kyori.indra.publishing"

  indra {
    javaVersions {
      testWith(8, 11, 16)
    }

    github("KyoriPowered", "examination") {
      ci(true)
    }
    mitLicense()

    configurePublications {
      pom {
        developers {
          developer {
            id = "kashike"
            timezone = "America/Vancouver"
          }
        }
      }
    }
  }

  repositories {
    mavenCentral()
  }

  dependencies {
    checkstyle("ca.stellardrift:stylecheck:0.1")
    annotationProcessor("ca.stellardrift:contract-validator:1.0.1") // https://github.com/zml2008/contract-validator
    testImplementation("com.google.truth:truth:1.1.3")
    testImplementation("com.google.truth.extensions:truth-java8-extension:1.1.3")
    testImplementation("org.junit.jupiter:junit-jupiter-api:5.7.2")
    testImplementation("org.junit.jupiter:junit-jupiter-engine:5.8.2")
  }
}
