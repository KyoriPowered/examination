buildscript {
  repositories {
    maven {
      url 'https://plugins.gradle.org/m2/'
    }
  }

  dependencies {
    classpath 'gradle.plugin.net.kyori:invent:1.0.4'
    classpath 'gradle.plugin.net.minecrell:licenser:0.4.1'
  }
}

subprojects {
  apply plugin: 'java-library'
  apply plugin: 'maven-publish'
  apply plugin: 'signing'
  apply plugin: 'net.kyori.invent'
  apply plugin: 'net.kyori.invent-javadoc-jar'
  apply plugin: 'net.kyori.invent-sonatype-repository'
  apply plugin: 'net.kyori.invent-sources-jar'
  apply plugin: 'net.minecrell.licenser'

  group 'net.kyori'
  version '1.0.0-SNAPSHOT'

  tasks.withType(JavaCompile) {
    options.compilerArgs += ['-Xlint:all']
  }

  license {
    header rootProject.file('header.txt')
    include '**/*.java'
    newLine false
  }

  repositories {
    mavenLocal()
    mavenCentral()
  }

  dependencies {
    api 'org.checkerframework:checker-qual:2.8.2'
    testImplementation 'com.google.truth:truth:0.42'
    testImplementation 'com.google.truth.extensions:truth-java8-extension:0.42'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.3.1'
    testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.3.1'
  }

  inventSonatypeRepository.enabled = System.getenv('TRAVIS') == null || project.version.endsWith('-SNAPSHOT')

  publishing {
    publications {
      maven(MavenPublication) {
        from components.java

        artifact javadocJar
        artifact sourcesJar

        pom {
          name = project.name
          description = project.description

          developers {
            developer {
              id = 'kashike'
            }
          }

          issueManagement {
            system = 'GitHub Issues'
            url = 'https://github.com/KyoriPowered/examination/issues'
          }

          licenses {
            license {
              name = 'MIT License'
              url = 'https://opensource.org/licenses/MIT'
            }
          }

          scm {
            connection = 'scm:git@github.com:KyoriPowered/examination.git'
            developerConnection = 'scm:git@github.com:KyoriPowered/examination.git'
            url = 'https://github.com/KyoriPowered/examination/'
          }
        }
      }
    }
  }

  signing {
    required { project.hasProperty('signing.keyId') && gradle.taskGraph.hasTask(':publish') && project.version.endsWith('-SNAPSHOT') }
    sign publishing.publications.maven
  }

}
