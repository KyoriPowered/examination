plugins {
  alias(libs.plugins.indra) apply false
  alias(libs.plugins.indra.checkstyle) apply false
  alias(libs.plugins.indra.licenser.spotless) apply false
  alias(libs.plugins.indra.sonatype)
}

group 'net.kyori'
version '1.3.1-SNAPSHOT'
description "A library for examining objects and producing an output - a friendly way to create a toString result."

indraSonatype {
  useAlternateSonatypeOSSHost("s01")
}

subprojects {
  apply plugin: "net.kyori.indra"
  apply plugin: "net.kyori.indra.checkstyle"
  apply plugin: "net.kyori.indra.licenser.spotless"
  apply plugin: "net.kyori.indra.publishing"

  indra {
    javaVersions {
      testWith(8, 11, 17)
    }
    checkstyle(libs.versions.checkstyle.get())

    github("KyoriPowered", "examination") {
      ci(true)
    }
    mitLicense()

    configurePublications {
      pom {
        developers {
          developer {
            id = "kashike"
            timezone = "America/Vancouver"
          }
        }
      }
    }
  }

  dependencies {
    checkstyle(libs.stylecheck)
    annotationProcessor(libs.contractValidator)
    testImplementation(libs.truth)
    testImplementation(libs.truth.java8)
    testImplementation(platform(libs.junit.bom))
    testImplementation(libs.junit.api)
    testImplementation(libs.junit.engine)
  }

  spotless {
    ratchetFrom 'origin/main'

    java {
      endWithNewline()
      indentWithSpaces 2
      importOrderFile rootProject.file('.spotless/kyori.importorder')
      trimTrailingWhitespace()
    }
  }
}
