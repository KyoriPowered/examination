buildscript {
  ext {
    indraVersion = "1.2.1"
  }
}

plugins {
  id "net.kyori.indra" version "$indraVersion" apply false
  id "net.kyori.indra.checkstyle" version "$indraVersion" apply false
  id "net.kyori.indra.license-header" version "$indraVersion" apply false
  id "net.kyori.indra.publishing.sonatype" version "$indraVersion" apply false
}

subprojects {
  apply plugin: "net.kyori.indra"
  apply plugin: "net.kyori.indra.checkstyle"
  apply plugin: "net.kyori.indra.license-header"
  apply plugin: "net.kyori.indra.publishing.sonatype"

  group 'net.kyori'
  version '1.1.0-SNAPSHOT'

  indra {
    javaVersions {
      testWith(8, 11, 15)
    }

    github("KyoriPowered", "examination") {
      ci = true
    }
    mitLicense()

    configurePublications {
      pom {
        developers {
          developer {
            id = "kashike"
            timezone = "America/Vancouver"
          }
        }
      }
    }
  }

  repositories {
    mavenLocal()
    mavenCentral()
  }

  dependencies {
    checkstyle("ca.stellardrift:stylecheck:0.1")
    testImplementation("com.google.truth:truth:1.1.1")
    testImplementation("com.google.truth.extensions:truth-java8-extension:1.1")
    testImplementation("org.junit.jupiter:junit-jupiter-api:5.7.0")
    testImplementation("org.junit.jupiter:junit-jupiter-engine:5.7.0")
  }
}
